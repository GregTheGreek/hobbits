request = request-line, lf, headers, body ;
response = response-line, lf, headers, body ;

request-line = magic , sp , version , sp , command , sp , compression-preferences , sp , headers-len , sp , body-len ;

response-status = digit , { digit } ;
response-line = response-status , sp , compression-preference , sp , headers-len , sp , body-len ;

magic = 'EWP' ;
version = '0.1' ;
uppercase-letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" ;
lowercase-letter = "a" | "b"
       | "c" | "d" | "e" | "f" | "g" | "h" | "i"
       | "j" | "k" | "l" | "m" | "n" | "o" | "p"
       | "q" | "r" | "s" | "t" | "u" | "v" | "w"
       | "x" | "y" | "z" ;
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
sp = " " ;
lf = "\n" ;

command-character = uppercase-letter | digit | "_" ;
command = command-character , { command-character } ;

compression-preference = lowercase-letter , { lowercase-letter } ;
compression-preferences = compression-preference , { ",", compression-preference } ;

headers-len = digit , { digit } ;
body-len = digit , { digit } ;

headers = ? BSON ? ;
body = ? BSON ? ;
