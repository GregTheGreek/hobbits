request = request-line, lf, headers, body ;
response = response-line, lf, headers, [ body ] ;

request-line = magic , sp , version , sp , command , sp , compression , sp , encoding , sp , headers-len , sp , body-len , [ sp , head-only-indicator ];

magic = 'EWP' ;
version = '0.2' ;

headers-len = digit , { digit } ;
body-len = digit , { digit } ;

command = command-character , { command-character } ;
command-character = uppercase-letter | digit | "_" ;

compression = compression-character , { compression-character } ;
compression-character = lowercase-letter | digit | "_" ;

encoding = encoding-character , { encoding-character } ;
encoding-character = lowercase-letter | digit | "_" ;

uppercase-letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" ;
lowercase-letter = "a" | "b"
       | "c" | "d" | "e" | "f" | "g" | "h" | "i"
       | "j" | "k" | "l" | "m" | "n" | "o" | "p"
       | "q" | "r" | "s" | "t" | "u" | "v" | "w"
       | "x" | "y" | "z" ;
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
sp = " " ;
head-only-indicator = "H" ;
lf = "\n" ;

headers = ? ENCODED DATA ? ;
body = ? COMMAND-DEFINED ENTITY ? ;
