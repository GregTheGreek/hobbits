envelope = info, lf, headers, body ;

info = magic , sp , version , sp , envelope-kind , sp , headers-len , sp , body-len;

magic = 'EWP' ;
version = '0.2' ;

headers-len = digit , { digit } ;
body-len = digit , { digit } ;

envelope-kind = envelope-kind-character , { envelope-kind-character } ;
envelope-kind-character = uppercase-letter | digit | "_" ;

uppercase-letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" ;
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
sp = " " ;
lf = "\n" ;

headers = ? BSON HEADER DATA ? ;
body = ? DATA PAYLOAD DEFINED BY envelope-kind elsewhere ? ;
